#
# Copyright © 2019-2022 Heinz Nixdorf Chair for Distributed Information Systems,
#                       Friedrich Schiller University Jena (http://www.fusion.uni-jena.de/)
# Copyright © 2023-2024 Jan Martin Keil (jan-martin.keil@uni-jena.de)
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

PREFIX av: <http://w3id.org/abecto/vocabulary#>
PREFIX dqv: <http://www.w3.org/ns/dqv#>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX wikibase: <http://wikiba.se/ontology#>

SELECT DISTINCT
	# TODO: adapt if https://phabricator.wikimedia.org/T313469 (allow reporting of mismatches for labels, descriptions and aliases) is done
	# TODO: adapt if qualifier omission supported by Mismatch Finder

	# expected format: https://github.com/wmde/wikidata-mismatch-finder/blob/main/docs/UserGuide.md

	(strAfter(str(?internal_url),"http://www.wikidata.org/entity/") AS ?item_id)
		# The item ID of the Wikidata item containing the mismatching statement.

	(replace(str(?statement),"http://www.wikidata.org/entity/statement/([0-9a-zA-Z]*)-(.*)","$1\\$$2") AS ?statement_guid)
		# (Optional) Represents that unique id of the statement on wikidata that contains the mismatching data.
		# If present, must be consistent with the item_id.
		# Can be empty to signify that no matching value was found on Wikidata, in which case the wikidata_value must also be empty.

	?property_id
		# The id of the wikidata property defining the wikidata value of the mismatch.

	(REPLACE(STR(?wikidata_valueX),"http://www.wikidata.org/entity/(Q\\d*)","$1") AS ?wikidata_value)
		# (Optional) The value on wikidata that mismatches an external database. Can be empty (see statement_guid).

	(REPLACE(STR(?meta_wikidata_valueX),"http://www.wikidata.org/entity/(Q\\d*)","$1") AS ?meta_wikidata_value)
		# (Optional) The value on wikidata that represents property calendar/time type.

	(STR(?external_valueX) AS ?external_value)
		# The value in the external database that mismatches a wikidata value.

	(STR(?external_urlX) AS ?external_url)
		# (Optional) A url or uri to the mismatching entity in the external database.

	?type
		# (Optional) A string that contains either the value 'statement' or the value 'qualifier' to indicate where the mismatch occurs.
		# If left empty a value of 'statement' will be assumed.

WHERE {
	BIND (<http://www.wikidata.org/> AS ?dataset)
	[]	a						av:AspectPattern ;
		av:associatedDataset	?dataset ;
		av:ofAspect				?aspect ;
		av:hasVariablePath		[ av:variableName ?variable ; av:propertyPath ?propertyPath ] .
	BIND (REPLACE(?propertyPath,"^.*/(P\\d*)>$","$1") AS ?property_id)
	FILTER REGEX(?property_id, "^P\\d*$", "i")
	BIND (IF(REGEX(?propertyPath, "<http://www.wikidata.org/prop/P\\d*>/<http://www.wikidata.org/prop/qualifier/P\\d*>", "i")
		    , true
		    , false
		    ) AS ?isQualifier)
	BIND (IF(?isQualifier,"qualifier","statement") AS ?type)
	BIND (IF(?isQualifier
	    	, IRI(REPLACE(?propertyPath,"^<(http://www.wikidata.org/prop/P\\d*)>","$1"))
	    	, IRI(CONCAT("http://www.wikidata.org/prop/",?property_id))
	    	) AS ?q)
	BIND (IF(?isQualifier
	    	, IRI(CONCAT("http://www.wikidata.org/prop/qualifier/",?property_id))
		    , IRI(CONCAT("http://www.wikidata.org/prop/statement/",?property_id))
		    ) AS ?ps_OR_pq)
	BIND (IF(?isQualifier
		    , IRI(CONCAT("http://www.wikidata.org/prop/qualifier/value/",?property_id))
		    , IRI(CONCAT("http://www.wikidata.org/prop/statement/value/",?property_id))
		    ) AS ?psv_OR_pqv)

	?issueGraph av:associatedDataset ?dataset .
	?sourceGraph av:associatedDataset ?dataset ;
		a av:PrimaryDataGraph .

	{ # Case: Deviation
		GRAPH ?issueGraph {
			[]	a						av:Deviation ;
				^oa:hasBody				[ oa:hasTarget ?internal_url ];
				av:affectedAspect		?aspect ;
				av:affectedVariableName	?variable;
				av:affectedValue		?wikidata_valueX ;
				av:comparedToValue		?external_valueX ;
				av:comparedToResource	?external_urlX .
		}
		GRAPH ?sourceGraph {
			?internal_url ?p ?statement .
			?statement ?ps_OR_pq ?wikidata_valueX .
			OPTIONAL {
				?statement ?psv_OR_pqv [ wikibase:timeCalendarModel ?meta_wikidata_valueX ] .
			}
		}
	}
	UNION
	{ # Case: Omission
		GRAPH ?issueGraph {
			[]	a						av:ValueOmission ;
				^oa:hasBody				[ oa:hasTarget ?internal_url ];
				av:affectedAspect		?aspect ;
				av:affectedVariableName	?variable;
				av:comparedToValue		?external_valueX ;
				av:comparedToResource	?external_urlX .
		}
	}
}
