#
# Copyright © 2019-2022 Heinz Nixdorf Chair for Distributed Information Systems,
#                       Friedrich Schiller University Jena (http://www.fusion.uni-jena.de/)
# Copyright © 2023-2024 Jan Martin Keil (jan-martin.keil@uni-jena.de)
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX av: <http://w3id.org/abecto/vocabulary#>
PREFIX dqv: <http://www.w3.org/ns/dqv#>
PREFIX oa: <http://www.w3.org/ns/oa#>

SELECT ?aspect ?missedInDataset ?missingResource (GROUP_CONCAT(?missingResourceLabel; SEPARATOR=", ") AS ?missingResourceLabelConcat) ?foundInDataset
WHERE {
  GRAPH ?graph {
    [ rdf:type  dqv:QualityAnnotation ;
      oa:hasBody 
        [ rdf:type               av:ResourceOmission ;
          av:affectedAspect      ?aspectResource ;
          av:comparedToDataset   ?foundInDataset ;
          av:comparedToResource  ?missingResource
        ] ;
      oa:hasTarget ?missedInDataset
    ] .
  }
  OPTIONAL { ?aspectResource rdfs:label ?aspectLabel .}
  OPTIONAL { 
      GRAPH ?sourceGraph {
          ?missingResource rdfs:label ?missingResourceLabel .
      }
      ?sourceGraph a av:PrimaryDataGraph ;
                   av:associatedDataset ?foundInDataset .
  }
  ?aspectResource av:keyVariableName ?aspectKeyVar .
  BIND(COALESCE(?aspectLabel, ?aspectKeyVar) AS ?aspect)
}
GROUP BY ?missedInDataset ?foundInDataset ?aspect ?missingResource
ORDER BY ?missedInDataset ?foundInDataset ?aspect ?missingResource
