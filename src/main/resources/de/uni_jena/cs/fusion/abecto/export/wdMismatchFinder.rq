PREFIX av: <http://w3id.org/abecto/vocabulary#>
PREFIX dqv: <http://www.w3.org/ns/dqv#>
PREFIX oa: <http://www.w3.org/ns/oa#>

SELECT
	# expected format https://github.com/wmde/wikidata-mismatch-finder/blob/main/docs/UserGuide.md
	?statement_guid # Represents that unique id of the statement on wikidata that contains the mismatching data.
	?property_id # The id of the wikidata property defining the wikidata value of the mismatch.
	(STR(?wikidata_valueX) AS ?wikidata_value) # The value on wikidata that mismatches an external database.
	(STR(?external_valueX) AS ?external_value) # The value in the external database that mismatches a wikidata value.
	(STR(?external_urlX) AS ?external_url) # (Optional) A url or uri to the mismatching entity in the external database.
WHERE {
	BIND (<http://www.wikidata.org/> AS ?dataset)
	[	a						av:AspectPattern ;
		av:associatedDataset	?dataset ;
		av:ofAspect				?aspect ;
		av:hasVariablePath		[ av:variableName ?variable ; av:propertyPath ?propertyPath ]
	]
	BIND (replace(str(?propertyPath),"<http://www.wikidata.org/prop/direct/(P\\d*)>","$1") AS ?property_id)
	?issueGraph dqv:computedOn ?dataset .
	{
		GRAPH ?issueGraph {
			[]	a						av:Deviation ;
				^oa:hasBody				[ oa:hasTarget ?internal_url ];
				av:affectedAspect		?aspect ;
				av:affectedVariableName	?variable;
				av:affectedValue		?wikidata_valueX ;
				av:comparedToValue		?external_valueX ;
				av:comparedToResource	?external_urlX .
		}
		BIND (IRI(CONCAT("http://www.wikidata.org/prop/",?property_id)) AS ?p)
		BIND (IRI(CONCAT("http://www.wikidata.org/prop/statement/",?property_id)) AS ?ps)
# TODO: add if https://phabricator.wikimedia.org/T312767 is fixed
#		OPTIONAL{
			GRAPH ?sourceGraph {
				?internal_url	?p	?statement .
				?statement		?ps	?wikidata_valueX .
				FILTER(regex(str(?statement),"http://www.wikidata.org/entity/statement/([0-9a-zA-Z]*)-(.*)"))
			}
			?sourceGraph a av:PrimaryDataGraph ;
						 av:associatedDataset ?dataset .
# TODO: add if https://phabricator.wikimedia.org/T312767 is fixed
#		}
#	} UNION {
#		GRAPH ?issueGraph {
#			[]	a						av:ValueOmission ;
#				^oa:hasBody				[ oa:hasTarget ?internal_url ];
#				av:affectedAspect		?aspect ;
#				av:affectedVariableName	?variable;
#				av:comparedToValue		?external_valueX ;
#				av:comparedToResource	?external_urlX .
#		}
	}
# TODO: remove workaround for https://phabricator.wikimedia.org/T312767
	BIND (replace(str(?statement),"http://www.wikidata.org/entity/statement/([0-9a-zA-Z]*)-(.*)","$1\\$$2") AS ?statement_guid)
# TODO: add if https://phabricator.wikimedia.org/T312767 is fixed
#	BIND (coalesce(
#		replace(str(?statement),"http://www.wikidata.org/entity/statement/([0-9a-zA-Z]*)-(.*)","$1\\$$2"),
#		strAfter(str(?internal_url),"http://www.wikidata.org/entity/") # fallback to entity id 
#	) AS ?statement_guid)
# TODO: replace if https://phabricator.wikimedia.org/T312851 is fixed
	FILTER REGEX(?property_id, "^P\\d*$", "i")
}
ORDER BY ?statement_guid ?property_id
